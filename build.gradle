plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id("io.papermc.paperweight.userdev") version "1.7.7"
}

group = 'net.vansen'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    paperweight.paperDevBundle("1.21.1-R0.1-SNAPSHOT")
    implementation "com.github.vansencool:FursConfig:1.0.3"
    implementation "com.github.vansencool:CommandUtils:1.2.3"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(List.of('paper-plugin.yml', "config.conf")) {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
    configurations = [project.configurations.runtimeClasspath]
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    destinationDirectory.set(file(System.getenv("BUILD_OUTPUT_DIR") ?: "build/libs"))
    minimize()
}

build {
    dependsOn shadowJar
}